// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/03/a/PC.hdl

/**
 * A 16-bit counter with load and reset control bits.
 * if      (reset[t] == 1) out[t+1] = 0
 * else if (load[t] == 1)  out[t+1] = in[t]
 * else if (inc[t] == 1)   out[t+1] = out[t] + 1  (integer addition)
 * else                    out[t+1] = out[t]
 */

CHIP PC {
    IN in[16],load,inc,reset;
    OUT out[16];

    PARTS:
    Inc16(in=regout, out=reginc);
    Mux16(a=regout, b=reginc, sel=inc, out=incmux);

    Mux16(a=incmux, b=in, sel=load, out=loadmux);
    
    Mux16(a=loadmux, b=false, sel=reset, out=resmux);

    Or8Way(in[0]=reset, in[1]=load, in[2]=inc, out=regload);

    Register(in=resmux, load=regload, out=regout, out=out);
}
